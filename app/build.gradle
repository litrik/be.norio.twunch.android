apply from: "$System.env.HOME" + "/norio.gradle"
apply plugin: 'android'

project.archivesBaseName="$appPackageName" + "-$appVersionName"

buildscript {
    repositories {
        maven {
            url 'https://raw.github.com/ark/ark/master/releases/'
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:0.5.+"
    }
}

dependencies {
    compile fileTree(dir: "libs", include: "*.jar")
    compile "com.android.support:support-v4:13.0.0"
    compile "com.actionbarsherlock:actionbarsherlock:4.3.2-SNAPSHOT"
    compile "com.google.android.gms:play-services:3.1.36"
    compile "com.squareup:otto:1.3.4"
    compile "com.jakewharton:butterknife:2.0.1"
}

configurations {
    all*.exclude group: "com.google.android", module: "android"
    all*.exclude group: "com.google.android", module: "support-v4"
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        versionCode Integer.valueOf("$appVersionCode")
        packageName "$appPackageName"
        versionName "$appVersionName"
        minSdkVersion 8
        targetSdkVersion 17
        buildConfig "public final static int VERSION_CODE = " + Integer.valueOf("$appVersionCode") + ";" +\
            "public final static String VERSION_NAME = \"" + "$appVersionName" + "\";"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.norio
            runProguard true
            proguardFile "proguard-project.txt"
        }
    }

    productFlavors {
        dev {
            buildConfig "public static final String PROVIDER_AUTHORITY = \"" + "$appPackageName" + ".dev\";"
        }
        prd {
            buildConfig "public static final String PROVIDER_AUTHORITY = \"" + "$appPackageName" + "\";"
        }
    }

}

android.productFlavors.each { flavor ->
    if (flavor.name != "prd") {
        flavor.packageName = android.defaultConfig.packageName + "." + flavor.name
    }
}
